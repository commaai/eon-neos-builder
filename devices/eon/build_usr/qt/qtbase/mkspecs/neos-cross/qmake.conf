ANDROID_BUILD_TOP=$$(ANDROID_BUILD_TOP)
isEmpty(ANDROID_BUILD_TOP) : error("$ANDROID_BUILD_TOP is empty. Forgot to run 'lunch'?")

ANDROID_PRODUCT_OUT=$$(ANDROID_PRODUCT_OUT)
isEmpty(ANDROID_PRODUCT_OUT) : error("$ANDROID_PRODUCT_OUT is empty. forgot to run 'lunch'?")

QT_QPA_DEFAULT_PLATFORM     = eglfs
EGLFS_DEVICE_INTEGRATION    = eglfs_surfaceflinger

MAKEFILE_GENERATOR      = UNIX
CONFIG                 += incremental
QMAKE_INCREMENTAL_STYLE = sublib

include(../common/linux.conf)
include(../common/gcc-base-unix.conf)
include(../common/clang.conf)

load(device_config)

ANDROID_PLATFORM        = android-24
CONFIG                 += $$ANDROID_PLATFORM

DEFINES                += Q_OS_ANDROID_EMBEDDED
CONFIG                 += android-embedded

QMAKE_CC                = aarch64-linux-android-clang
QMAKE_CXX               = aarch64-linux-android-clang++
QMAKE_LINK              = $$QMAKE_CXX
QMAKE_LINK_SHLIB        = $$QMAKE_CXX
QMAKE_AR                = aarch64-linux-android-ar cqs
QMAKE_OBJCOPY           = aarch64-linux-android-objcopy
QMAKE_NM                = aarch64-linux-android-nm -P
QMAKE_PKG_CONFIG        = aarch64-linux-android-pkg-config
QMAKE_STRIP             = 
#aarch64-linux-android-strip
QMAKE_RANLIB            = aarch64-linux-android-ranlib

QMAKE_CFLAGS           += -fstack-protector-strong -Oz -I/data/data/com.termux/files/usr/include
QMAKE_CXXFLAGS         += -fstack-protector-strong -Oz -I/data/data/com.termux/files/usr/include
QMAKE_LFLAGS           += -L$${ANDROID_PRODUCT_OUT}/obj/lib -L/data/data/com.termux/files/usr/lib -Wl,-rpath=/system/lib64:/data/data/com.termux/files/usr/lib -Wl,--enable-new-dtags -Wl,--as-needed -Wl,-z,relro,-z,now
QMAKE_LFLAGS_SHLIB     += -shared
QMAKE_LFLAGS_PLUGIN    += -shared

QMAKE_LIBS_THREAD       =

load(qt_config)
